/*
 * Copyright Terracotta, Inc.
 * Copyright Super iPaaS Integration LLC, an IBM Company 2024
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import static java.time.Instant.now

plugins {
  id 'java-library'
}

description = 'terracotta-core build data info'

def buildProperties = [
        version: project.version,
        timestamp : now(),
        revision : "Unknown",
        branch : "Unknown"
]

tasks.register('writeBuildInfo') {
    def dyn = layout.buildDirectory.dir("dynamic")
    outputs.dir(dyn)
    doLast {
        dyn.get().getAsFile().mkdir()
        dyn.get().file("build-data.txt").asFile.withPrintWriter("UTF-8") {
            it.println("terracotta.build.version=${buildProperties.version}")
            it.println("terracotta.build.revision=${buildProperties.revision}")
            it.println("terracotta.build.timestamp=${buildProperties.timestamp}")
            it.println("terracotta.build.branch=${buildProperties.branch}")
        }
    }
}

sourceSets {
    main {
        java {
            resources {
                srcDir writeBuildInfo
            }
        }
    }
}
