/*
 * Copyright Terracotta, Inc.
 * Copyright IBM Corp. 2024, 2025
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import org.terracotta.build.plugins.PackagePlugin

plugins {
    id 'org.terracotta.build.package' apply false
    id 'maven-publish'
    id 'com.jfrog.artifactory' version '5.2.5'
}

description = 'terracotta-core'

var publishApi = false

allprojects {
    PackagePlugin.augmentAttributeSchema(project)
    
    if (project.getPlugins().hasPlugin('com.jfrog.artifactory')) {
        project.getTasks().getByPath('publish').dependsOn(project.getTasks().getByPath('artifactoryPublish'))
    }

    if (project.hasProperty('overrideVersion')) {
      project.version = project.overrideVersion
    } else {
      project.version = "$project.defaultVersion"
    }
    project.setProperty("apiVersion", project.version)
}

/* 
    close off artifactory unless needed by the release process
*/

if (project.hasProperty('publishApi')) {
    println "publishing API with value " + project.getProperty('publishApi')
    publishApi = true
}

if (project.hasProperty('overrideVersion')) {
    artifactoryPublish.skip = true

    artifactory {
        contextUrl = "https://na.artifactory.swg-devops.com/artifactory" // Replace with your Artifactory URL
        publish {
            repository {
                releaseRepoKey = "hyc-webmriab-team-tc01-os-releases-maven-local"
                snapshotRepoKey = "hyc-webmriab-team-tc01-os-snapshots-maven-local" // The Artifactory repository key to publish to
                username = project.findProperty("artifactory_user") ?: System.getenv("ARTIFACTORY_DEPLOY_USERNAME")
                password = project.findProperty("artifactory_password") ?: System.getenv("ARTIFACTORY_DEPLOY_PASSWORD")
            }
            defaults {
                if (publishApi) {
                    publications('api', 'runtime')
                } else {
                    publications('runtime')
                }
            }
            buildInfo {
                buildName = "hyc-webmriab-team-tc01/terracotta/${rootProject.name}"
                buildNumber = rootProject.findProperty("buildNumber") ?: '' + new Random(System.currentTimeMillis()).nextInt(20000)
            }
        }
    }
}
