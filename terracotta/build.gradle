
plugins {
    id 'java-library'
    id 'distribution'
    id 'maven-publish'
}

description = 'terracotta-core terracotta server package'

base {
    archivesName = 'terracotta'
}

configurations {
  kit
  server
}

dependencies {
  testImplementation "junit:junit:$junitVersion"
  testImplementation "org.hamcrest:hamcrest-all:$hamcrestVersion"
  kit files(distZip)
  server project(":tc-server")
}

jar {
    archiveBaseName = "tc"
    archiveVersion = ""
    manifest {
        attributes "Main-Class" : "com.tc.server.TCServerMain", 
                    "Class-Path" : configurations.server.collect { it.getName() }.join(' ')
    }
}

task copyServer(type: Copy) {
    into "${buildDir}/libs"
    from project(":tc-server").configurations.runtimeClasspath
}

build.dependsOn(copyServer)

distributions {
  main {
    contents {
      includeEmptyDirs = false

      into("server") {
        into("lib") {
          from "${buildDir}/libs/tc.jar"
          from(project(":tc-server").configurations.runtimeClasspath)
        }
      }
    }
  }
}

distTar.dependsOn(jar)
distZip.dependsOn(jar)

artifacts {
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'org.terracotta.internal'
            version = "$project.version"
            artifactId = "$project.name"
            from components.java
        }
    }
    repositories {
        maven {
            name = ''
            url = ''
        }
    }
}


