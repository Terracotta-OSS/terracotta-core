/*
 * Copyright Terracotta, Inc.
 * Copyright Super iPaaS Integration LLC, an IBM Company 2024
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'java-library'
    id 'maven-publish'
}

description = 'terracotta-core galvan'

configurations {
    terracottaKit
}

dependencies {
    api project(":test-interfaces")
    implementation project(":server-api")
    implementation "org.terracotta:ipc-eventbus:1.1.4"
    api "org.terracotta:terracotta-utilities-port-chooser:0.0.19"
    api "junit:junit:$junitVersion"

    testImplementation "org.mockito:mockito-inline:4.11.0"
    
    terracottaKit project(":tc-server")
    terracottaKit project(":terracotta")
}

def galvanKit = tasks.register('galvanServerZip', Zip) {
    destinationDirectory = layout.buildDirectory.dir('galvan-kit')
    archiveFileName = "galvan-test-server.zip"
    into("server") {
        from(configurations.terracottaKit)
    }
}

sourceSets.main {
    resources.srcDir(galvanKit.map { it.destinationDirectory })
}

tasks.named('test', Test) {
    systemProperty "kitInstallPath", layout.buildDirectory.dir("galvan-default").get().toString()
}

publishing {
    publications {
        runtime(MavenPublication) {
            groupId = 'org.terracotta'
            version = "$project.version"
            artifactId = "$project.name"
            from components.java
        }
    }
    repositories {
        maven {
            name = 'releases'
            url = 'https://repo.terracotta.org/maven2'
        }
    }
}


