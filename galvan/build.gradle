/*
 * Copyright Terracotta, Inc.
 * Copyright Super iPaaS Integration LLC, an IBM Company 2024
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'java-library'
    id 'maven-publish'
    id 'distribution'
}

description = 'terracotta-core galvan'

base {

}

jar {

}

configurations {
    terracottaKit
}

def serverDistribution = project(":terracotta").getLayout().getBuildDirectory().file("distributions")

dependencies {
    api project(":test-interfaces")
    implementation project(":terracotta")
    implementation project(":server-bootstrap")
    implementation "org.terracotta:ipc-eventbus:1.1.4"
    api "org.terracotta:terracotta-utilities-port-chooser:0.0.19"
    implementation "junit:junit:$junitVersion"
    testImplementation "ch.qos.logback:logback-classic:$logbackVersion"
    implementation "org.mockito:mockito-inline:4.11.0"
    implementation project(':examples:clustered-map:client')
    terracottaKit project(":tc-server")
    terracottaKit project(":terracotta")
}

distributions {
  main {
    distributionBaseName = 'galvan-test-server'
    contents {
      includeEmptyDirs = false

      into("server") {
        from(configurations.terracottaKit)
      }
    }
  }
}

task copyServer(type: Copy) {
    from (layout.buildDirectory.dir("distributions")) {
        include "*.zip"
    }
    into layout.buildDirectory.dir("resources/main")
    rename { distName ->
        distName.replace("-$project.version", "")
    }
}

test {
    systemProperty "kitInstallPath", layout.buildDirectory.dir("galvan-default").get().toString()
}

copyServer.dependsOn(distZip)
compileJava.dependsOn(copyServer)

publishing {
    publications {
        runtime(MavenPublication) {
            groupId = 'org.terracotta'
            version = "$project.version"
            artifactId = "$project.name"
            from components.java
        }
    }
    repositories {
        maven {
            name = 'releases'
            url = 'https://repo.terracotta.org/maven2'
        }
    }
}


