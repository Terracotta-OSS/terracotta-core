/*
 * Copyright Terracotta, Inc.
 * Copyright Super iPaaS Integration LLC, an IBM Company 2024
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'java-library'
    id 'maven-publish'
}

version = "$project.apiVersion"
description = 'terracotta-core server-api'

base {

}

configurations {
    apiJar
    embeddedJar {
      attributes {
        attribute(Bundling.BUNDLING_ATTRIBUTE, objects.named(Bundling, Bundling.EMBEDDED))
      }
    }
}
  

dependencies {
  compileOnlyApi "org.slf4j:slf4j-api:$slf4jVersion"
  compileOnlyApi "ch.qos.logback:logback-classic:$logbackVersion"
  
  api project(':common-api')

  apiJar ("org.terracotta:entity-server-api:$terracottaApisVersion") {
      exclude group: "org.terracotta", module: "entity-common-api"
  }
  apiJar "org.terracotta:monitoring-support:$terracottaApisVersion"
  apiJar "org.terracotta:standard-cluster-services:$terracottaApisVersion"
  
  apiJar project(":tripwire:plugin")
  apiJar project(':server-spi')
  apiJar project(':configuration-provider')
  apiJar project(':server-bootstrap')
}

task unzipApiJars(type: Copy) {
    dependsOn configurations.apiJar
    def target = layout.buildDirectory.dir("classes/java/main")
    configurations.apiJar.each {
        println it
        from zipTree(it)
    }
    exclude "META-INF/**"
    into target
    duplicatesStrategy DuplicatesStrategy.WARN
}

compileJava.dependsOn(unzipApiJars)

artifacts {
    embeddedJar jar
}

publishing {
    publications {
        api(MavenPublication) {
            groupId = 'org.terracotta'
            version = "$project.version"
            artifactId = "$project.name"
            from components.java
        }
    }
    repositories {
        maven {
            name = 'releases'
            url = 'https://repo.terracotta.org/maven2'
        }
    }
}


